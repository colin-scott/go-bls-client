# Note you need gnuplot 4.4 for the pdfcairo terminal.

set terminal pdfcairo font "Gill Sans,9" linewidth 2 fontscale 1.0
#set terminal pdfcairo font "Gill Sans,9" linewidth 4 rounded fontscale 1.0
# set terminal pdf font "Helvetica, 12" linewidth 4 # size 200,100

# Line style for axes
set style line 80 lt rgb "#808080"

# Line style for grid
set style line 81 lt 0  # dashed
set style line 81 lt rgb "#808080"  # grey

set grid back linestyle 81
set border 3 back linestyle 80 # Remove border on top and right.  These
             # borders are useless and make it harder
             # to see plotted lines near the border.
    # Also, put it in grey; no need for so much emphasis on a border.
set xtics nomirror
set ytics nomirror

#set log y
#set mytics 10    # Makes logscale look good.

# Line styles: try to pick pleasing colors, rather
# than strictly primary colors or hard-to-see colors
# like gnuplot's default yellow.  Make the lines thick
# so they're easy to see in small plots in papers.
set style line 3 lt rgb "#00A000" lw 2 pt 0
set style line 2 lt rgb "#A00000" lw 2 pt 0
set style line 1 lt rgb "#5060D0" lw 2 pt 0
set style line 4 lt rgb "#F25900" lw 2 pt 0

set key bottom right
set key samplen 2 spacing .9 font ",8"

set ylabel "Number of Employees in the US Normalized Per Industry"
set xlabel "Year"

set xdata time
set timefmt "%Y%m"
set format x "%Y"

set output "oes_normalized.pdf"

# Note that we're leaving out output, title, and plot
#set output "runtime_graph.pdf"
plot 'Software_Industry.dat.normalized' using 1:2 title "Software Industry" w lp ls 1, \
     'Education.dat.normalized' using 1:2 title "Postsecondary CS teachers/researchers" w lp ls 2, \
     'Research_Lab.dat.normalized' using 1:2 title "Research Scientists (Industrial)" w lp ls 3
